augment Routine
  # Hook into BuildGDExtend.rogue functionality
  GLOBAL METHODS
    method rogo_default [insert]
      rogo_build_rogue

    method patch_gitignore( patcher:GitIgnorePatcher ) [append]
      patcher.add( "# Plasmacore", "/Source/Generated" )
endAugment

routine rogo_build_rogue
  rogo_compile_rogue
endRoutine

routine rogo_compile_rogue( platform="":String )
  if (String.exists(platform))
    # Re-capitalize 'platform'
    local platforms = ["Android","iOS","Linux","macOS","UWP","Web","Windows"]
    forEach (p in platforms)
      if (platform.equals(p,&ignore_case))
        platform = p
        escapeForEach
      endIf
    endForEach
  else
    platform = System.os
  endIf

  local output = File( "Source/Generated" )
  output.create_folder
  local cmd = "roguec Source/Main.rogue Source/Plasmacore --output=$/RogueProgram"(output)
  cmd .= appending( "--target=$,Plasmacore,C++"(platform) )
  if (platform == "macOS" or platform == "iOS") cmd += ",ObjC"
  execute cmd
endRoutine
