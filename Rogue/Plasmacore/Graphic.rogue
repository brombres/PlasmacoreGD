module Plasmacore

class Graphic [essential]
  GLOBAL PROPERTIES
    lookup       : [String:GDObject]
    next_z_index : Int32

    active_list : Graphic[]

  GLOBAL METHODS
    method on_begin_update
      ensure<<active_list>>
      (forEach in active_list).is_presented = false

    method on_end_update
      ensure<<active_list>>
      forEach (graphic in rewriter=active_list.rewriter)
        if (graphic.is_presented)
          # Keep in the active list
          rewriter.write( graphic )
        else
          # Will be removed from the active list
          graphic.is_active = false
          graphic.is_visible = false
        endIf
      endForEach

  PROPERTIES
    node : GDObject

    position : XYZ
    scale    = XYZ(1,1,1)
    rotation : XYZ

    base_size : XY

    is_active    : Logical
    is_presented : Logical

  METHODS
    method init( name:String )
      if (not lookup) lookup = [String:GDObject]

      contingent
        node = lookup[ name ]
        sufficient (node)

        if (Plasmacore.gd_graphics) node = Plasmacore.gd_graphics[name]
        sufficient (node)

        if (Plasmacore.gd_assets) node = Plasmacore.gd_assets[name]
        sufficient (node)

        if (Plasmacore.gd_config) node = Plasmacore.gd_config[name]
        sufficient (node)

        node = Plasmacore.gd_scene[name]
      endContingent

      if (node)
        lookup[ name ] = node
        init( node.cloned )
      endIf

    method init( node )
      if (node)
        Plasmacore.gd_config.add( node )
        node.pixel_scale = 1.0  # only affects Sprite3D
        base_size = find_base_size
      endIf

    method cloned->Graphic
      return Graphic( node.cloned.[is_visible=false] )

    method on_cleanup
      delete

    method find_base_size->XY
      if (node.has_method("get_size"))
        return node( "get_size" )
      endIf

      if local size = node.meta( "size" )
        return size
      endIf

      if local frames = node.sprite_frames
        if local frame = frames.texture("",0)
          return frame.size
        endIf
      endIf

      return XY(100,100)

    method delete
      if (node)
        if (node.parent)
          node.parent.remove( node )
        endIf
        node.delete
        node = GDObject()
      endIf

    method is_visible->Logical
      return node.visible

    method present
      if (not is_active)
        ensure<<active_list>>
        active_list.add( this )
        is_visible = true
        is_active = true
      endIf

      if (Plasmacore.is_2dx)
        node.position = XYZ( position.x, Display.size.y - position.y )
      else
        node.position = position
      endIf
      node.scale = scale
      node.rotation = rotation
      if (Plasmacore.is_2d)
        node.z_index = next_z_index
        ++next_z_index
      endIf

      is_presented = true

    method set_is_visible( setting:Logical )
      node.visible = setting

    method set_scale( s:Real64 )
      @scale = XYZ( s, s, s )

    method set_scale( @scale )

      #method set_size( sz:Real64 )
      #set_size( XYZ(sz,sz,sz) )

endClass
