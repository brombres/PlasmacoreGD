module RogueGD

uses Geometry [export]
uses Graphics [export]
uses Math/Best

$include RogueGD/App
$include RogueGD/Box
$include RogueGD/Display
$include RogueGD/ScreenState
$include RogueGD/Godot
$include RogueGD/View
$include RogueGD/ViewState

class RogueGD [essential api singleton]
  PROPERTIES
    rogue_gd         : GDObject
    message_handlers = [String:Function(GDVariant)->GDVariant]
    views            = View[]

  METHODS
    method init
      Global.output = Log
      Global.error = ErrorLog
      on_message( "RogueGD.configure", (args) => RogueGD.configure(args) )
      on_message( "RogueGD.update", (args) => RogueGD.update(args) )

    method configure( args:GDVariant )->GDVariant
      rogue_gd = args[0]
      ScreenState.on_configure

      return $defaultValue<<GDVariant>>

    method on_message( name:String, handler:Function(GDVariant)->GDVariant )
      message_handlers[ name ] = handler

    method on_message( name:GDVariant, args:GDVariant )->GDVariant
      local handler = message_handlers[ name ]
      if (handler) return handler( args )
      else         return GDVariant()

    method register( view:View )
      if (not views.contains(view)) views.add( view )

      on_message( "$.configure"(view.type_name), view=>configure )
      on_message( "$.draw"(view.type_name), view=>draw )
      on_message( "$.update"(view.type_name), view=>update )

    method update( args:GDVariant )->GDVariant
      rogue_gd = args[0]
      Display.dt = args[1]
      ScreenState.update

      return $defaultValue<<GDVariant>>

endClass
