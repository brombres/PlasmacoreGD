module Plasmacore

NATIVE_POINTER_TYPE<<NativeGDNode,"godot::Node*">>

class Node( gd_node:NativeGDNode, type:NodeType ) [compound]
  GLOBAL METHODS
    method create( gd_node:NativeGDNode )->Node
      if (not gd_node.exists) return Node( gd_node, NodeType )

      local type = NodeType
      if (instance_of<<Sprite3D>>(gd_node)) type = NodeTypeSprite3D
      if (instance_of<<Node3D>>(gd_node))   type = NodeTypeNode3D

      return Node( gd_node, type )

    method create<<$ClassName>>->Node
      local gd_node : NativeGDNode
      native @|$gd_node = memnew($ClassName)
      return Node( gd_node )

    method instance_of<<$GodotType>>( gd_node:NativeGDNode )->Logical
      local result : Logical
      native @|$result = !!godot::Object::cast_to<godot::$GodotType>( $gd_node.value );
      return result

  METHODS
    method cloned->Node
      if (not gd_node.exists) return this

      local result : NativeGDNode
      native @|$result = (NativeGDNode){ $gd_node.value->duplicate() };
      return Node( result, type )

    method add( child:Node )
      native @|$gd_node.value->add_child( $child.gd_node.value );

    method class_name->String
      local result : String
      native @|$result = RogueString_create( $gd_node.value->get_class().utf8().get_data() );
      return result

    method count->Int32
      local result : Int32
      native @|$result = $gd_node.value->get_child_count();
      return result

    method filepath->String
      local result : String
      native @|$result = RogueString_create( $gd_node.value->get_scene_file_path().utf8().get_data() );
      return result

    method get( index:Int32 )->Node
      local child : NativeGDNode
      native @|$child = (NativeGDNode){ $gd_node.value->get_child( $index ) };
      return Node( child )

    method get( node_path:String )->Node
      local result : NativeGDNode
      if (gd_node.exists)
        native @|godot::NodePath path( $node_path->data->as_utf8 );
                |$result = (NativeGDNode){ $gd_node.value->get_node_or_null( path ) };
      endIf
      return Node( result )

    method instance_of<<$GodotType>>->Logical
      local result : Logical
      native @|$result = !!godot::Object::cast_to<godot::$GodotType>( $gd_node.value );
      return result

    method last->Node
      return this[ count-1 ]

    method name->String
      local result : String
      native @|$result = RogueString_create( ((godot::String)$gd_node.value->get_name()).utf8().get_data() );
      return result

    method operator?->Logical
      return gd_node.exists

    method remove( child:Node )
      native @|$gd_node.value->remove_child( $child.gd_node.value );

    method queue_free
      if (not gd_node.exists) return
      native @|$gd_node.value->queue_free() };

    method set_position( xy:RealXY )
      type.set_position( gd_node, xy )

    method set_rotation( z:Real64 )
      set_rotation( XYZ(0,0,z) )

    method set_rotation( xyz:XYZ )
      type.set_rotation( gd_node, xyz )

    method set_scale( xy:RealXY )
      type.set_scale( gd_node, xy )

    method set_name( new_name:String )
      native @|String new_name( $new_name );
              |$gd_node.value->set_name( new_name );

    method set_visible( setting:Logical )
      type.set_visible( gd_node, setting )

    method to->String
      return type->String( gd_node )

endClass

class NodeType [singleton]
  METHODS
    method set_position( gd_node:NativeGDNode, xy:RealXY )
      noAction

    method set_rotation( gd_node:NativeGDNode, xyz:XYZ )
      noAction

    method set_scale( gd_node:NativeGDNode, xy:RealXY )
      noAction

    method set_visible( gd_node:NativeGDNode, setting:Logical )
      noAction

    method to->String( gd_node:NativeGDNode )
      return Node( gd_node, this ).name

endClass

class NodeTypeNode3D : NodeType [singleton]
  METHODS
    method set_position( gd_node:NativeGDNode, xy:RealXY )
      local z = -2.0
      native
        @|godot::Vector3 position( (float)$xy.x, (float)$xy.y, (float)$z );
         |godot::Object::cast_to<godot::Node3D>( $gd_node.value )->set_position( position );

    method set_rotation( gd_node:NativeGDNode, xyz:XYZ )
      native
        @|godot::Vector3 rotation( (float)$xyz.x, (float)$xyz.y, (float)$xyz.z );
         |godot::Object::cast_to<godot::Node3D>( $gd_node.value )->set_rotation( rotation );

    method set_scale( gd_node:NativeGDNode, xy:RealXY )
      local z = which{ xy.x==xy.y:xy.x || 1.0 }
      native
        @|godot::Vector3 scale( (float)$xy.x, (float)$xy.y, (float)$z );
         |godot::Object::cast_to<godot::Node3D>( $gd_node.value )->set_scale( scale );

    method set_visible( gd_node:NativeGDNode, setting:Logical )
      native
        @|godot::Object::cast_to<godot::Node3D>( $gd_node.value )->set_visible( $setting );
      noAction
endClass

class NodeTypeSprite3D : NodeTypeNode3D [singleton]
endClass
